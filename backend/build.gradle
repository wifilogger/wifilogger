apply plugin: 'com.android.library'

buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:2.3.3'
    }
}

allprojects {
    repositories {
        jcenter()
    }
    configurations {
        checkstyleClasspath
    }
    dependencies {
        checkstyleClasspath 'com.puppycrawl.tools:checkstyle:7.8.1'
    }

    task checkstyle(type: Checkstyle) {
        configFile = rootProject.file("config/checkstyle/checkstyle.xml")
        source 'src'
        include '**/*.java'
        exclude '**/gen/**'

        classpath = files()
        checkstyleClasspath = project.configurations.checkstyleClasspath

        maxWarnings 1

        reports {
            xml.enabled true
            xml.destination new File("$project.buildDir/reports/checkstyle/checkstyle_report.xml")

            html.enabled true
            html.destination new File("$project.buildDir/reports/checkstyle/checkstyle_report.html")
        }
    }
}

build.dependsOn 'checkstyle'

android {
    compileSdkVersion 26
    buildToolsVersion "26.0.0"
    defaultConfig {
        minSdkVersion 14
        targetSdkVersion 23
        versionCode 1
        versionName "1.0"
    }
    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

dependencies {
    testCompile 'junit:junit:4.12'

    compile 'org.slf4j:slf4j-api:1.7.21'
    compile 'com.github.tony19:logback-android-core:1.1.1-6'
    compile 'com.github.tony19:logback-android-classic:1.1.1-6'
}
